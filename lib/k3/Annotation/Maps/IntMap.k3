include "Annotation/Map.k3"

// Maps are expected to contain only records with fields key and value
//
// This map is implemented with a backend type specialized for int keys
// While there currently is no type enforcement, it should only be used
// with key :: int
//
annotation IntMap given type a,b,c,d
{
  provides lifted size : () -> int
    with effects \_ -> R[self]

  provides lifted clear : () -> ()
    with effects \_ -> [R[self]; W[self]]

  //////////////////////////////////////////////////////////
  // Map retrieval.
  // These methods ignore the value of the provided element.

  @:CArgs 2
  provides lifted peek : (() -> a) -> (content -> a) -> a
    with effects \f -> \g -> [R[self]; f content; g content]

  provides lifted member: content -> bool
    with effects \elem -> [R[self]; R[elem]]

  @:CArgs 3
  provides lifted lookup : content -> (() -> a) -> (content -> a) -> a
    with effects \elem -> \noneF -> \someF -> [R[self]; R[elem]; noneF content; someF content]

  @:CArgs 3
  provides lifted lookup_key : int -> (() -> a) -> (content -> a) -> a
    with effects \key -> \noneF -> \someF -> [R[self]; R[key]; noneF content; someF content]


  ///////////////////////////////////////////////////////
  // Map modification.

  provides lifted insert  : content -> ()
    with effects \elem -> [R[self]; R[elem]; W[self]]

  @:CArgs 2
  provides lifted update  : content -> content -> ()
    with effects \oldElem -> \newElem -> [R[self]; R[oldElem]; R[newElem]; W[self]]

  @:CArgs 2
  provides lifted update_key  : int -> content -> ()
    with effects \key -> \newElem -> [R[self]; R[key]; R[newElem]; W[self]]

  provides lifted erase  : content -> ()
    with effects \elem -> [R[self]; R[elem]; W[self]]
  
  provides lifted erase_key : int -> ()
    with effects \key -> [R[self]; R[key]; W[self]]

  @:CArgs 3
  provides lifted erase_with : int -> (() -> a) -> (content -> a) -> a
    with effects \key -> \noneF -> \someF -> [R[self]; R[key]; W[self]; noneF none; someF content]

  @:CArgs 2
  provides lifted insert_with: content -> (content -> content -> content) -> ()
    with effects \elem -> \f -> [R[self]; R[elem]; ((f content) content); W[self]]

  @:CArgs 3
  provides lifted upsert_with: content -> (() -> content) -> (content -> content) -> ()
    with effects \elem -> \f -> \g -> [R[self]; R[elem]; f none; g content; W[self]]

  @:CArgs 3
  provides lifted upsert_with_key: int -> (() -> content) -> (content -> content) -> ()
    with effects \key -> \f -> \g -> [R[self]; R[key]; f none; g content; W[self]]


  //////////////////////////////////////////////////////////////
  // Bulk transformations.

  @:Transformer
  provides lifted combine : self -> self
    with effects \other -> [R[self]; R[other]]

  @:Transformer
  provides lifted split : () -> (self, self)
    with effects \_ -> R[self]

  @:Transformer
  provides lifted iterate : (content -> ()) -> ()
    with effects \iterF -> [R[self]; ([R[content]; iterF content])*]

  @:Transformer
  provides lifted map : (content -> {key: int, value: a}) -> collection {key: int, value: a} @IntMap
    with effects \mapF -> [R[self]; ([R[content]; mapF content])*]

  @:Transformer
  provides lifted map_generic : (content -> {key: a, value: b}) -> collection {key: a, value: b} @Map
    with effects \mapF -> [R[self]; ([R[content]; mapF content])*]

  @:Transformer
  provides lifted filter : (content -> bool) -> self
    with effects \filterF -> [R[self]; ([R[content]; filterF content])*]

  @:{Transformer, CArgs 2}
  provides lifted fold : (a -> (content -> a)) -> a -> a
    with effects \foldF -> \z -> [R[self]; R[z]; ([R[content]; ((foldF z) content)])*]

  @:{Transformer, CArgs 3}
  provides lifted group_by : (content -> int) -> (a -> content -> a) -> a
                              -> collection { key : int, value : a } @IntMap
    with effects
      \gbF -> \gaccF -> \z ->
        [R[self]; R[z]; ([R[content]; gbF content; ((gaccF z) content)])*]

  @:{Transformer, CArgs 3}
  provides lifted group_by_generic : (content -> a) -> (b -> content -> b) -> b
                                      -> collection { key : a, value : b } @Map
    with effects
      \gbF -> \gaccF -> \z ->
        [R[self]; R[z]; ([R[content]; gbF content; ((gaccF z) content)])*]

  @:{Transformer, CArgs 4}
  provides lifted group_by_contiguous : (content -> int) -> (a -> content -> a) -> a -> int
                                         -> collection { key : int, value : a } @IntMap
    with effects
      \gbF -> \gaccF -> \z -> \s ->
        [R[self]; R[z]; R[s]; ([R[content]; gbF content; gaccF content])*]

  @:Transformer
  provides lifted ext : (content -> collection {key : int, value: a} @IntMap)
                         -> collection {key : int, value: a} @IntMap
    with effects \extF -> [R[self]; ([R[content]; extF content])*]

  @:Transformer
  provides lifted ext_generic : (content -> collection {key : a, value: b} @Map)
                                 -> collection {key : a, value: b} @Map
    with effects \extF -> [R[self]; ([R[content]; extF content])*]

  // Mosaic-specific functionality.

  // A fused equi-join and fold, taking a RHS collection, a key extractor
  // that must produce the same type as the map's key, and an accumulator
  // and initial value.
  @:{Transformer, CArgs 4}
  provides lifted equijoinkf_kv : collection {key: a, value: b} @Collection
                                -> ({key: a, value: b} -> c)
                                -> (d -> content -> {key: a, value: b} -> d) -> d -> d
    with effects \other -> \otherKeyF -> \foldF -> \z ->
                   [R[self]; R[z]; ([R[other]; R[content]; otherKeyF none; ((foldF z) content) none])*]


}
