include "Annotation/Collection.k3"
include "Annotation/Map.k3"
include "Annotation/Seq.k3"
include "Annotation/Vector.k3"
include "Core/Barrier.k3"
include "Core/Profile.k3"
include "Distributed/Transformers.k3"
include "tpch/schema.k3"
include "tpch/loader.k3"

declare ignore : forall a. a -> () = \_ -> ()

control TPCHBenchmarkWithoutMaster
                     [nodes              : expr,
                      loadExpr           : expr,
                      preLoadExpr        : expr,
                      preReadyExpr       : expr,
                      onReadyExpr        : expr,
                      finishArgT         : type,
                      preTestFinishExpr  : expr,
                      preFinishExpr      : expr,
                      preShutdownExpr    : expr]
{
  ?e @:Result => (finished, master) <- $.[e]
  ?e @:Start  => (load_all, me)     <- $.[e]
    +> {
      // Generic, single relation dataset
      declare dataFiles : collection {path: string} @Collection

      // Relation-specific datasets
      declare lineitemFiles : collection {path : string} @Collection
      declare customerFiles : collection {path : string} @Collection
      declare ordersFiles   : collection {path : string} @Collection
      declare supplierFiles : collection {path : string} @Collection
      declare partFiles     : collection {path : string} @Collection
      declare partsuppFiles : collection {path : string} @Collection
      declare nationFiles   : collection {path : string} @Collection
      declare regionFiles   : collection {path : string} @Collection

      declare index_by_hash : forall a. a -> int = (\s -> (
        let n = $[nodes].size () in
        let h = (hash s) in
        ((h % n) + n) % n
      ))

      // Dummy trigger to warm up connections
      trigger hello : () = \_ -> ()

      // A shutdown trigger which runs at each peer to terminate the distributed program.
      trigger shutdown : () = \_ -> (
        $[preShutdownExpr];
        haltEngine ()
      )

      // A 'finished' trigger which shuts down all the nodes. This is invoked at the master.
      trigger finished : $[finishArgT] = \finishArg -> (
        ($[preTestFinishExpr] finishArg);
        ((( $[preFinishExpr];
            $[nodes].iterate (\p -> (shutdown, p.addr) <- ()))
              @:StopTimer) @Time(lbl=[# query], tag=[$ "Query time"])
        ) @OnCounter(id=[# query_done], eq=[$ $[nodes].size()], reset=[$ false], profile=[$ false])
      )

      // A 'ready' trigger which starts the query once all nodes are alive. This is invoked at the master.
      trigger ready : () = \_ -> (
        ( $[preReadyExpr];
          (( $[onReadyExpr] @:StartTimer) @Time(lbl=[# query], tag=[$ "Query time:"])
          ) @OnCounter(id=[#  nodes_ready], eq=[$ $[nodes].size()], reset=[$ false], profile=[$ false]))
      )

      trigger load_all : () = \_ -> (
        $[preLoadExpr];
        ($[loadExpr]) @Profile(lbl=[# loader], tag=[$ "Load time"]);
        ((ready, master) <- ())
      )
    }
}

control TPCHBenchmark[nodes              : expr,
                      loadExpr           : expr,
                      preLoadExpr        : expr,
                      preReadyExpr       : expr,
                      onReadyExpr        : expr,
                      finishArgT         : type,
                      preTestFinishExpr  : expr,
                      preFinishExpr      : expr,
                      preShutdownExpr    : expr]
{
  ?e => ($.[e]) @TPCHBenchmarkWithoutMaster(
                      nodes             = nodes
                    , loadExpr          = loadExpr
                    , preLoadExpr       = preLoadExpr
                    , preReadyExpr      = preReadyExpr
                    , onReadyExpr       = onReadyExpr
                    , finishArgT        = finishArgT
                    , preTestFinishExpr = preTestFinishExpr
                    , preFinishExpr     = preFinishExpr
                    , preShutdownExpr   = preShutdownExpr )
     +> { declare master : address = 127.0.0.1:40000 }
}
