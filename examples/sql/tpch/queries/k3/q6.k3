include "Annotation/Collection.k3"
include "Core/Builtins.k3"
include "tpch/schema.k3"
include "tpch/loader.k3"
include "tpch/benchmark.k3"


declare lineitem : lineitem_bag
declare q6_result : mut real = 0.0

trigger q6_local : () = \_ -> (
  (ignore
    ((( (lineitem
        .filter  (\r ->     ( r.l_shipdate >= "1994-01-01" )
                        and ( r.l_shipdate < "1995-01-01" )
                        and ( r.l_discount >= ( 0.06 - 0.01 ) )
                        and ( r.l_discount <= ( 0.06 + 0.01 ) )
                        and ( r.l_quantity < 24 )
                  )
        ).fold    (\acc -> \r -> acc + (r.l_extendedprice * r.l_discount)) 0.0
      ) 
      ) @:Result) @TPCHBenchmark)
)

trigger start : () = \_ -> (() @:Start) @TPCHBenchmark(
  nodes              = [$ peers],
  onReadyExpr        = [$ peers.iterate (\x -> (q6_local, x.addr) <- () ) ],
  loadExpr           = [$ dataFiles.iterate (\e -> lineitemLoaderP e.path lineitem)],
  preLoadExpr        = [$ ()],
  preReadyExpr       = [$ ()],
  finishArgT         = [: real],
  preTestFinishExpr  = [$ (\x -> q6_result = q6_result + x)],
  preFinishExpr      = [$ print (concat "Result: " (rtos q6_result))],
  preShutdownExpr    = [$ ()]
)

source rows : () = value ()
feed rows |> start
