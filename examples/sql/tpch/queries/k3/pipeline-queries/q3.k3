include "tpch/benchmark.k3"

/**********************
* SQL query
***********************
select top 10
        l_orderkey,
        sum(l_extendedprice * (1 - l_discount)) as revenue,
        o_orderdate,
        o_shippriority
from
        customer,
        orders,
        lineitem
where
        c_mktsegment = 'BUILDING'
        and c_custkey = o_custkey
        and l_orderkey = o_orderkey
        and o_orderdate < date '1995-03-15'
        and l_shipdate > date '1995-03-15'
group by
        l_orderkey,
        o_orderdate,
        o_shippriority
order by
        revenue desc,
        o_orderdate
****************************/

declare master : address

declare lineitem : mut lineitem_bag
declare customer : mut customer_bag
declare orders   : mut orders_bag

// Final result.
typedef lior_epd_by_ods_r   = {key : {ok: int, od: string, sp: int}, value: real}
typedef lior_epd_by_ods_map = collection lior_epd_by_ods_r @Map

declare q3_peer_result : lior_epd_by_ods_map

// Intermediates for Q3 plans.
typedef li_epd_by_ok_r        = {key: int , value: real }  // l_orderkey => l_extendedprice * (1 - l_discount)
typedef lior_epdods_by_ck_r   = {key: int, value : lior_epd_by_ods_map }
typedef lior_epdods_by_ck_map = collection lior_epdods_by_ck_r @Map

typedef cs_ck_bag = collection {elem: {c_custkey : int}} @Collection

// Plan intermediates.
declare lior_epdods_map  : lior_epdods_by_ck_map
declare c_ck_bag         : mut collection {c_custkey : int} @Collection

// Pipelining flags.
declare customers_sent       : mut bool = false
declare customers_done       : mut bool = false
declare lineitem_orders_done : mut bool = false

declare merge_li_epd_by_ok : li_epd_by_ok_r -> li_epd_by_ok_r -> li_epd_by_ok_r =
  \a -> \b -> {key : a.key, value : a.value + b.value}

declare merge_lior_epd_by_ods : lior_epd_by_ods_r -> lior_epd_by_ods_r -> lior_epd_by_ods_r =
  \a -> \b -> {key : a.key, value : a.value + b.value}

declare merge_lior_r : lior_epdods_by_ck_r -> lior_epdods_by_ck_r -> lior_epdods_by_ck_r =
  \a -> \b -> {key : a.key, value : a.value.fold (\acc -> \r -> ((acc.insert_with r merge_lior_epd_by_ods); acc)) b.value}

// Distributed plan: dist. hash-join on li-or, broadcast semi-join on lior-cs.
trigger lineitem_orders : () = \_ -> (
  ( () @:NoSkew )
    @JoinSelector
      ( lbl                  = [# lo ]
      , lhs_ht_id            = [# lo_lhs_ht ]
      , rhs_ht_id            = [# lo_rhs_ht ]

      , lhs_sample_query     = [$ lineitem.sample (\acc -> \l ->
                                                    if l.l_shipdate > "1995-03-15" then
                                                      ((acc.insert_with
                                                        {key: l.l_orderkey.b, value: 1}
                                                        (\old -> \new -> {key: old.key, value: old.value + new.value}));
                                                       acc)
                                                    else acc
                                                  )
                                                  (empty {key: int, value: int} @Map)
                                                  (lineitem.size() / j1_sample_factor)
                               ]

      , rhs_sample_query     = [$ orders.sample (\acc -> \o ->
                                                  if o.o_orderdate < "1995-03-15" then
                                                    ((acc.insert_with
                                                      {key: o.o_orderkey, value: 1}
                                                      (\old -> \new -> {key: old.key, value: old.value + new.value}));
                                                     acc)
                                                  else acc
                                                )
                                                (empty {key: int, value: int} @Map)
                                                (orders.size() / j1_sample_factor)
                               ]

      , frequency_threshold  = [$ 1000.0 ]

      , lhs_query = [$  // TODO: this currently does not fuse with the group-by in SendPartitionByKey.
                        // We need a nested fusion class structure, i.e., ICond1(DCond2), for this functionality.
                        ((lineitem.filter  (\l -> l.l_shipdate > "1995-03-15" ))
                                  .groupBy (\l -> l.l_orderkey)
                                           (\acc -> \l -> acc + (l.l_extendedprice * (1 - l.l_discount)))
                                           0.0)
                                  .fold    (\acc -> \l -> ((acc.insert l); acc)) (empty {key:int, value:real} @Map)
                    ]

      , rhs_query = [$ ((orders.filter  (\o -> o.o_orderdate < "1995-03-15" ))
                               .groupBy (\o -> o.o_orderkey)
                                        (\_ -> \o -> { od : o.o_orderdate
                                                     , ck : o.o_custkey
                                                     , sp : o.o_shippriority})
                                        { od: "", ck: 0, sp: 0 })
                               .fold    (\acc -> \o -> ((acc.insert o); acc))
                                        (empty {key : int, value : {od:string, ck:int, sp:int}} @Map)
                    ]

      , join_key_ty          = [: int ]
      , lhs_ht_ty            = [: {key:int, value: real} ]
      , rhs_ht_ty            = [: {key:int, value: {od:string, ck:int, sp:int} } ]

      , lhs_probe            = [$ (\lht -> \_ -> \o ->
                                    case lht.lookup {key: o.key, value: 0.0}
                                    of
                                      { Some li -> lior_epdods_map.insert_with
                                                    { key: o.value.ck
                                                    , value: {|key : {ok: int, od: string, sp: int}, value: real
                                                              | { key   : {ok: o.key, od: o.value.od, sp: o.value.sp }
                                                                , value : li.value }
                                                              |} @Map }
                                                    merge_lior_r
                                      }
                                      { None   -> ()  }
                                  )
                               ]

      , rhs_probe            = [$ (\rht -> \_ -> \l ->
                                    case rht.lookup {key: l.key, value: {od:"", ck:0, sp:0}}
                                    of
                                      { Some ord -> lior_epdods_map.insert_with
                                                     { key: ord.value.ck
                                                     , value: {|key : {ok: int, od: string, sp: int}, value: real
                                                               | { key   : {ok: l.key, od: ord.value.od, sp: ord.value.sp }
                                                                 , value : l.value }
                                                               |} @Map }
                                                     merge_lior_r
                                      }
                                      { None   -> ()  }
                                  )
                               ]

      , lhs_insert_with      = [$ merge_li_epd_by_ok ]
      , rhs_insert_with      = [$ (\_ -> \new -> new) ]

      , has_outputs          = [$ (\_ -> not customers_sent) ]
      , empty_out_buffer     = [$ () ]

      , lhs_pipeline_next    = [$ (\_ -> if not customers_sent then ( ((lineitem_orders_customer, me) <- ());
                                                                      customers_sent = true)
                                                               else () )
                               ]
      , rhs_pipeline_next    = [$ (\_ -> if not customers_sent then ( ((lineitem_orders_customer, me) <- ());
                                                                      customers_sent = true)
                                                               else () )
                               ]

      , lhs_clear_expr       = [$ (\_ -> lo_lhs_ht = empty {key:int, value: real} @Map) ]
      , rhs_clear_expr       = [$ (\_ -> lo_rhs_ht = empty {key:int, value: {od:string, ck:int, sp:int}} @Map) ]

      , peer_next            = [$ (loc_probe_lhs, me) <- () ]

      , next                 = [$ () ]
      , coordinator          = [$ master]
      , nodes                = [$ peers]
      , profile              = [$ false]
      )
)

trigger lineitem_orders_customer : () = \_ -> (
  (print "loc");
  ( (() @:StartTimer) @Time(lbl=[# loc_JOIN], tag=[$ "Time "]) );
  // Broadcast customers to all peers with li-or buckets for semi-join.
  ignore (((customer.filter  (\c -> c.c_mktsegment == "BUILDING" ))
            .map     (\c -> { c_custkey : c.c_custkey }))) @ BroadcastByMachine
                                                          ( lbl = [# loc_broadcast]
                                                          , dest_trg = [$ loc_probe_rhs]
                                                          , masters = masters
                                                          , send_ty = [$ cs_ck_bag]
                                                          , send_default = [$ empty cs_ck_bag ]
                                                          , send_extra_fn = [$ \x -> x ]
                                                          )
)

// TODO: sort and limit on q3_peer_result
trigger loc_probe_lhs : () = \_ -> (
  (print "loc_probe_lhs");
  ( c_ck_bag.iterate (\c ->
    case lior_epdods_map.lookup {key: c.c_custkey, value: empty lior_epd_by_ods_r @Map}
    of
    { Some lo_r -> lo_r.value.iterate (\kv -> q3_peer_result.insert_with kv merge_lior_epd_by_ods) }
    { None -> () } )
  );
  (if customers_done then (
      ( (() @:Result) @TPCHBenchmarkWithoutMaster );
        if master == me then ( () @:StopTimer) @Time(lbl=[# loc_JOIN], tag=[$ "Time "]) else ()
  ) else ());
  c_ck_bag = empty {c_custkey : int} @Collection;
  (print "lo done");
  lineitem_orders_done = true
)

trigger loc_probe_rhs : cs_ck_bag = (\vals -> (
  (print "loc_probe_rhs");
  (if lineitem_orders_done then (
    vals.iterate (\v ->
      case lior_epdods_map.lookup {key: v.elem.c_custkey, value: empty lior_epd_by_ods_r @Map}
      of
      { Some lo_r -> lo_r.value.iterate (\kv -> q3_peer_result.insert_with kv merge_lior_epd_by_ods) }
      { None -> () } )
  ) else ( vals.iterate (\v -> c_ck_bag.insert v.elem) )) ;
  ( print  "outside customers counter");
  (
    (print "customers counter done!");
    ( customers_done = true );
    if lineitem_orders_done then (
      ( (() @:Result) @TPCHBenchmarkWithoutMaster );
        if master == me then ( () @:StopTimer) @Time(lbl=[# loc_JOIN], tag=[$ "Time "]) else ()
    ) else ()
  ) @OnCounter(id=[# loc_done], eq=[$ peers.size()], reset=[$ false], profile=[$ false])
)) @ DispatchByMachine
   ( lbl = [# loc_broadcast]
   , dest_trg = loc_probe_rhs
   , send_ty = [$ cs_ck_bag]
   , send_default = [$ empty cs_ck_bag]
   )

// For ktrace:
declare results: collection {orderkey: int, revenue: real, orderdate: string, shippriority: int} @Collection

trigger start : () = \_ -> (() @:Start) @TPCHBenchmarkWithoutMaster(
  nodes                = [$ peers ],
  loadExpr             = [$ ( lineitemFiles.iterate (\e -> lineitemLoaderP e.path lineitem);
                              ordersFiles.iterate   (\e -> ordersLoaderP   e.path orders);
                              customerFiles.iterate (\e -> customerLoaderP e.path customer)
                            ) ],
  preLoadExpr          = [$ ()],
  preReadyExpr         = [$ ()],
  onReadyExpr          = [$ (lineitem_orders, me) <- ()],
  finishArgT           = [: ()],
  preTestFinishExpr    = [$ (\_ -> ())],
  preFinishExpr        = [$  () @DistributedCollectionStatistics(
                                    lbl         = [# q3_skew ],
                                    query_cl    = [* [% cl: [# loc_skew], ce: [$ lior_epdods_map]]
                                                  ],
                                    coordinator = [$ master ],
                                    nodes       = [$ peers ],
                                    next        = [$ peers.iterate (\p -> (shutdown, p.addr) <- ()) ])
                         ],
  preShutdownExpr      = [$ q3_peer_result.iterate (\r ->
                                results.insert { orderkey: r.key.ok
                                               , revenue: r.value
                                               , orderdate: r.key.od
                                               , shippriority: r.key.sp })
                         ],
  finishAsShutdownExpr = [$ false]
)

source rows : () = value ()
feed rows |> start
