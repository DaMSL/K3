include "Annotation/Collection.k3"
include "Annotation/Vector.k3"
include "Core/Builtins.k3"
include "Core/CppBuiltins.k3"

declare master: address = 127.0.0.1:40000
declare num_peers: mut int = 1
declare peers_ready: mut int = 0
declare iterations_remaining: mut int = 10
declare dimensionality: int = 2

declare start_ms: mut int
declare end_ms: mut int
declare elapsed_ms: mut int

// Hyperparameters
declare step_size: mut real = 0.1
declare lambda: mut real = 0.1

// Mast1
declare aggregates: mut {count: int, sum: collection {elem: real} @Vector }

// All Nodes:
declare parameters: mut collection {elem: real} @Vector
declare data: collection { elem: collection {elem: real}@Vector, class_label: real } @ { Collection }

// SVM Loss at 1 point.
declare svm_loss : collection {elem: real} @Vector  -> real -> real = \x -> \y -> (
  let q = 1 - (y * (parameters.dot x))
  in (lambda * (parameters.dot parameters) + (if q < 0 then 0 else q))
)

// SVM Loss averaged over data
declare svm_loss_avg : () -> real = \_ -> (
  let stats =
    data.fold
      (\acc -> \d ->
        {count: acc.count +1, sum: acc.sum + (svm_loss d.elem d.class_label)}
      )
      {count:0, sum:0.0}
  in stats.sum / stats.count
)

// SVM gradient at 1 point.
declare svm_gradient : collection {elem: real} @Vector   -> real -> collection {elem: real} @Vector  = \x -> \y -> (
  let flag = y * (parameters.dot x)
  in
    if flag > 1
    then parameters.scalarMult lambda
    else (parameters.scalarMult lambda).sub (x.scalarMult y)
)

// Compute the gradient at a single point.
// The gradient is a function of the current parameters, a global variable.
declare point_gradient: collection {elem: real} @Vector  -> real -> collection {elem: real} @Vector  = \point -> \class_label -> (
    svm_gradient point class_label
)

// Update parameters based on a single point
declare update_parameters: collection {elem: real} @Vector  -> real -> () = \point -> \class_label -> (
  let update = (point_gradient point class_label).scalarMult step_size in
  parameters.inPlaceSub update
)

// Print results
declare print_results : () -> () = \_ -> (
  end_ms = now_int ();
  elapsed_ms = end_ms - start_ms;
  print (itos elapsed_ms)
)

// Run SGD on local dataset.
// 0) Receive updated parameters from master
// 1) Update local parameters once per data point (randomly ordered/drawn)
// 2) Send local parameters to master for aggregation
trigger local_sgd : collection {elem: real} @Vector  = \new_params -> (
  (parameters = new_params);
  (data.iterate (\d -> update_parameters d.elem d.class_label));
  (aggregate, master) <- parameters
)

// Barrier: Stash local aggregates until all peers are finished. Then maximize.
trigger aggregate : collection {elem: real} @Vector  = \local_params -> (
  let new_sum = local_params.add (aggregates.sum) in
  let new_count = 1 + aggregates.count
  in (
    aggregates = {count: new_count, sum: new_sum};
    if new_count == num_peers
    then (maximize,master) <- ()
    else ()
  )
)


// TODO: more in place operations on the parameters
// consider inPlaceScalarMult
// Average local parameters from each peer, then start a new iteration.
trigger maximize : () = \_ -> (
  parameters = (aggregates.sum).scalarMult (1.0 / aggregates.count);
  print "Loss:";
  print (rtos (svm_loss_avg ()));
  step_size = 0.95 * step_size;
  aggregates = {count:0, sum: (zeroVector dimensionality)};
  iterations_remaining = iterations_remaining - 1;
  if iterations_remaining == 0
  then (print_results (); peers.iterate (\p -> (shutdown, p.addr) <- ()))
  else peers.iterate (\p -> (local_sgd, p.addr) <- parameters)
)

// TODO: random instead of zero
// Randomly initialize parameters, and send them to each peer.
trigger start : () = \_ -> (
  aggregates = {count:0, sum: zeroVector dimensionality};
  start_ms = now_int ();
  parameters = zeroVector dimensionality;
  peers.iterate (\p -> (local_sgd, p.addr) <- parameters)
)

trigger ready : () = \_ -> (
  (peers_ready = peers_ready + 1);
  (if peers_ready == num_peers
   then (start, master) <- ()
   else ()
  )
)

trigger shutdown : () = \_ -> haltEngine ()

// TODO: fix loader
trigger load_all : () = \_ -> (
  ()
  //LoaderVectorLabel "/Users/joshwheeler/foo.lvector" data;
  //(ready, master) <- ()
)

source points : () = value ()
feed points |> load_all
