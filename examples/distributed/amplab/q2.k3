include "Annotation/Collection.k3"
include "Core/CppBuiltins.k3"

// Hack
declare dataLoader : string -> collection {
    sourceIP: string,
    destURL: string,
    visitDate: string,
    adRevenue: real,
    userAgent: string,
    countryCode: string,
    languageCode: string,
    searchWord: string,
    duration: int
    } @ { Collection } -> ()


// Constants
declare master: address = 127.0.0.1:40000
declare x: mut int = 10
declare num_peers: int = 2
declare data_file : string = "/k3/data/amplab/rankings_10.k3"

// Globals
declare peers_ready: mut int = 0
declare peers_finished: mut int = 0

// Data
declare local_uservisits: collection {
    sourceIP: string,
    destURL: string,
    visitDate: string,
    adRevenue: real,
    userAgent: string,
    countryCode: string,
    languageCode: string,
    searchWord: string,
    duration: int
    } @ { Collection }

declare local_q1_results: collection {key: string, value: real} @ { Collection }

// Time
declare start_ms : mut int = 0
declare end_ms : mut int = 0
declare elapsed_ms : mut int = 0

// Perform the query on the local dataset. (Filter and project.)
// Store the results in memory.
trigger q2_local : () = \_ -> (
  let agg_vals =
    local_uservisits.groupBy
      (\r -> substring r.sourceIP 1 x)
      (\acc -> \r ->
        acc + r.adRevenue)
      0.0
  in
  (aggregate, master) <- agg_vals 
)

declare master_agg : mut collection { key: string, value: real } @ {Collection}

trigger aggregate : collection { key: string, value: real } @ {Collection} = \vals -> (
  let local_agg = master_agg.combine vals in
  let local_agg' = local_agg.groupBy 
        (\r -> r.key)
        (\acc -> \r -> acc + r.value)
        0.0
  in
  master_agg = local_agg';
  peers_finished = peers_finished + 1;
  if peers_finished == num_peers
  then end_ms = now ();
       elapsed_ms = end_ms - start_ms;
       printLine (itos elapsed_ms);
       peers.iterate (\p -> (shutdown, p.addr) <- ())
  else ()
)

trigger shutdown : () = \_ -> (
  haltEngine ()
)

// Signal to the master that a peer is ready.
// Once all peers are ready, the master will start the query.
trigger ready : () = \_ -> (
  peers_ready = peers_ready + 1;
  if peers_ready == num_peers
  then start_ms = now (); peers.iterate (\p -> (q2_local, p.addr) <- ())
  else ()
)

trigger load_all : () = \_ -> (
  (dataLoader data_file local_uservisits);
  ((ready, master) <- ())
)

source rows : () = value ()
feed rows |> load_all
