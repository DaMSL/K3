include "amplab/AmplabBDB.k3"
include "Annotation/Set.k3"
include "Core/IO.k3"

// Globals
declare thresh   : mut int = 1000 // pageRank threshold for filter
declare num_results : mut int = 0

// Data
declare local_rankings  :     collection {pageRank: int, pageURL: string} @Collection
typedef q1_result = collection {pageRank: int, pageURL: string} @Collection
declare results         :  mut q1_result

// Perform the query on the local dataset. (Filter and project.)
// Store the results in memory.
trigger q1_local: () = \_ -> (
  results = local_rankings.filter (\row -> row.pageRank > thresh);
  ignore ((results.size ()) @:Result) @AmplabBenchmark
)

declare finalize : collection {key: address, value: q1_result} @Map -> () = \x -> (
  (x.iterate (\t ->
    t.value.iterate (\v ->
      (results_sink, me) <- (v.pageURL, v.pageRank)
    )
  ))
)

trigger start : () = \_ -> (() @:Start) @AmplabBenchmark(
  queryTrigger       = [$ q1_local],
  loadExpr           = [$ loadQ1 rankingsFiles local_rankings ],
  preLoadExpr        = [$ ()],
  preReadyExpr       = [$ peers.iterate (\p -> (hello, p.addr) <- ())],
  finishArgT         = [: int],
  preTestFinishExpr  = [$ (\num_peer_results -> num_results = num_results + num_peer_results)],
  preFinishExpr      = [$ (ignore results) @GatherCollection(
                                                 lbl   = [# result],
                                                 nodes = [$ peers],
                                                 next  = [$ (\x -> (finalize x; peers.iterate (\p -> (shutdown, p.addr) <- () )))])
                       ],
  preShutdownExpr    = [$ ()]
)


sink query_time_sink : int = file "time.csv" text csv
sink results_sink : (string, int) = file "results.csv" text psv

source rows: () = value ()
feed rows |> start
