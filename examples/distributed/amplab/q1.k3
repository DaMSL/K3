include "Annotation/Collection.k3"
include "Core/Builtins.k3"

// Generate a builtin to load data into a collection
@:CArgs 2
declare dataLoader: string -> collection {pageURL: string,pageRank: int, avgDuration: int} @ { Collection } -> ()

// Constants
declare master: address = 127.0.0.1:40000
declare x: mut int = 10 //pageRank threshold for the select statement
declare rankings_file: string = "/k3/data/amplab/rankings_10.k3"

// Globals
declare peers_ready: mut int = 0
declare peers_finished: mut int = 0
declare num_results: mut int = 0

// Data
declare local_rankings: collection {pageRank: int, pageURL: string, avgDuration: int} @ { Collection }
declare local_q1_results: collection {pageRank: int, pageURL: string} @ { Collection }

// Time
declare start_ms: mut int = 0
declare end_ms: mut int = 0
declare elapsed_ms: mut int = 0

// Perform the query on the local dataset. (Filter and project.)
// Store the results in memory.
trigger q1_local: () = \_ -> (
  local_rankings.iterate (\row ->
    if row.pageRank > x
    then local_q1_results.insert ({pageRank: row.pageRank, pageURL: row.pageURL})
    else ()
  );
  (finished, master) <- (local_q1_results.size ())
)

// Signal to the master that a peer has finished the query locally.
trigger finished: int = \num_peer_results -> (
  num_results = num_results + num_peer_results;
  peers_finished = peers_finished + 1;
  if peers_finished == peers.size ()
  then end_ms = now_int ();
       elapsed_ms = end_ms - start_ms;
       print (concat "# Results: " (itos num_results));
       print (concat "Elapsed time (ms):" (itos elapsed_ms));
       peers.iterate (\p -> (shutdown, p.addr) <- ())
  else ()
)

trigger shutdown: () = \_ -> (
  haltEngine ()
)

// Signal to the master that a peer is ready.
// Once all peers are ready, the master will start the query.
trigger ready: () = \_ -> (
  peers_ready = peers_ready + 1;
  if peers_ready == peers.size ()
  then start_ms = now_int (); peers.iterate (\p -> (q1_local, p.addr) <- ())
  else ()
)

trigger load_all: () = \_ -> (
  (dataLoader rankings_file local_rankings);
  ((ready, master) <- ())
)

source rows: () = value ()
feed rows |> load_all
