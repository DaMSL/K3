include "Annotation/Collection.k3"
include "Annotation/Seq.k3"
include "Annotation/Map.k3"
include "Core/Builtins.k3"
include "Core/Loaders.k3"
include "Core/MachineMessaging.k3"
include "Core/Barrier.k3"

declare master: address = 127.0.0.1:30001
declare peer_seq : collection {addr: address} @ {Seq}

declare index_by_hash: string -> int = \s -> (
  let n = peer_seq.size () in
  let h = (hash s) in
  ((h % n) + n) % n
)

declare start_ms : mut int
declare end_ms : mut int
declare elapsed_ms : mut int

typedef uv_record = {
    sourceIP: string,
    destURL: string,
    visitDate: string,
    adRevenue: real
}

declare user_visits: collection uv_record @ { Collection }

typedef rankings_record = {
    pageURL: string,
    pageRank: int,
    avgDuration: int
}

typedef rk_map_record = {key: string, value: int}
//key: pageURL, value: pageRank
@:Pinned
declare rankingsMap : collection rk_map_record @ Map

declare empty_source_ip_to_aggs_C : collection  {key: string, value: {prSum: int, prCount: int, arSum: real}} @ Map
declare empty_aggs : {prSum: int, prCount: int, arSum: real}

typedef Source_ip_to_aggs = {key: string, value: {prSum: int, prCount: int, arSum: real}}

typedef Source_ip_to_aggs_C = collection  {key: string, value: {prSum: int, prCount: int, arSum: real}} @ Map

declare partial_aggs : collection {key: int, value: Source_ip_to_aggs_C} @ Map


declare merged_partials : Source_ip_to_aggs_C

declare date_lb : string = "1980-01-01"
declare date_ub : string = "1980-04-01"
declare valid_date : string -> bool = \i -> (i <= date_ub and i >= date_lb)

declare flag : mut bool = false
declare flag2 : mut bool = false

trigger hello : () = \_ -> (
  ((ready, master) <- ()) @OnCounter(id=[# hello_done], eq=[$ peers.size()], reset=[$ false], profile=[$ false])
)

trigger start_profiler : () = \_ -> (
  ((tcmallocStart ()); (jemallocStart ()); (pcmStart ())) @IfMachineMaster
)

trigger q3_local : () = \_ -> (
  user_visits.iterate (\uv ->
    if (valid_date uv.visitDate)
    then (
      let ip = uv.sourceIP in
      let url = uv.destURL in
      let a = index_by_hash ip in
      case partial_aggs.lookup {key: a, value: empty_source_ip_to_aggs_C} of
      { Some aggs ->
        case rankingsMap.lookup {key: url, value: 0} of
        {Some r ->
          case (aggs.value.lookup {key: ip, value: empty_aggs}) of
          {Some pa_v ->
             aggs.value.insert
             {key: ip,
              value: {prSum: pa_v.value.prSum + r.value,
                      prCount: pa_v.value.prCount + 1,
                      arSum: pa_v.value.arSum + uv.adRevenue
                      }
              }
          }
          { None ->
              aggs.value.insert {key: ip, value: {prSum: r.value, prCount: 1, arSum: uv.adRevenue} }
          };

          partial_aggs.insert {key: a, value: aggs.value}

        }
        { None -> () }
      }
      { None ->
        case rankingsMap.lookup {key: url, value: 0} of
        {Some r ->
          partial_aggs.insert
          {key: a, value:
            {|key: string, value: {prSum: int, prCount: int, arSum: real}|
              {key: ip, value: {prSum: r.value, prCount: 1, arSum: uv.adRevenue} }
            |} @ Map
          }

        }
        { None -> () }
      }


    )
    else ()
  );
  partial_aggs.iterate (\kv -> (merge, (peer_seq.at kv.key).addr) <- kv.value);
  peers.iterate (\p -> (count_merges, p.addr) <- ())
)

trigger merge : Source_ip_to_aggs_C = \aggs_map -> (
  aggs_map.iterate (\kv ->
    case merged_partials.lookup {key: kv.key, value: kv.value} of
    {Some agg ->
      merged_partials.insert
        {key: kv.key,
         value:
          {prSum: agg.value.prSum + kv.value.prSum,
           prCount: agg.value.prCount + kv.value.prCount,
           arSum: agg.value.arSum + kv.value.arSum
          }
        }
    }
    {None -> merged_partials.insert kv}


  )
)

declare merged_peers : mut int = 0

trigger count_merges : () = \_ -> (
  merged_peers = merged_peers + 1;
  if merged_peers == (peers.size ())
  then ((find_local_max, me) <- ())
  else ()

)

declare local_max : mut Source_ip_to_aggs

trigger find_local_max : () = \_ -> (
  merged_partials.iterate (\kv ->
    if (kv.value.arSum > local_max.value.arSum)
    then (local_max = kv)
    else ()
  );
  (find_global_max, master) <- local_max
)

declare peers_done : mut int = 0
declare global_max : mut Source_ip_to_aggs
declare results: collection {sourceIP: string, totalRevenue: real, avgPageRank: real} @Collection

trigger find_global_max :  Source_ip_to_aggs = \kv -> (
  (if (kv.value.arSum > global_max.value.arSum)
  then (global_max = kv)
  else ());
  peers_done = peers_done + 1;
  if (peers_done == peers.size ())
  then ((finished, master) <- ())
  else ()
)

typedef Source_ip_to_aggs = {key: string, value: {prSum: int, prCount: int, arSum: real}}


trigger finished : () = \_ -> (
  end_ms = now_int ();
  elapsed_ms = end_ms - start_ms;
  print ("Time Query:" ++ (itos elapsed_ms));
  print ("Global max.key: " ++ global_max.key);
  results.insert {sourceIP: global_max.key, totalRevenue: global_max.value.arSum, avgPageRank: (1.0 * global_max.value.prSum) / global_max.value.prCount};
  peers.iterate (\p -> (shutdown, p.addr) <- ())
)

declare peers_ready : mut int = 0
trigger ready: () = \_ -> (
  peers_ready = peers_ready + 1;
  if peers_ready == peers.size ()
  then (
    (peers.iterate (\p -> (start_profiler, p.addr) <- () ));
    start_ms = now_int (); peers.iterate (\p -> (q3_local, p.addr) <- ()) )
  else ()
)

declare uvFiles : collection {path: string} @Collection

declare rkFiles : collection {path: string} @Collection

trigger loadAll : () = \_ -> (
  peers.iterate (\p -> peer_seq.insert p);
  loadUVQ3 uvFiles user_visits;
  loadRKQ3 rkFiles rankingsMap;
  peers.iterate (\p -> (hello, p.addr) <- ())
)

trigger shutdown : () = \_ -> (
  ((tcmallocStop ()); (jemallocStop ()); (pcmStop ())) @IfMachineMaster;
  haltEngine ()
)

source rows : () = value ()
feed rows |> loadAll
