include "Annotation/Collection.k3"

declare lowerDate: int
declare upperDate: int

declare master: address = localhost:90000

declare peer_count: int = 5

declare userVisits_raw: collection {
    sourceIP: string,
    destURL: string,
    visitDate: string,
    adRevenue: real,
    userAgent: string,
    countryCode: string,
    languageCode: string,
    searchWord: string,
    duration: int
} @ { Collection }

declare userVisits: collection {
    sourceIP: string,
    destURL: string,
    visitDate: int,
    adRevenue: real,
    userAgent: string,
    countryCode: string,
    languageCode: string,
    searchWord: string,
    duration: int
} @ { Collection }

declare rankings: collection {
    pageURL: string,
    pageRank: int,
    avgDuration: int
} @ { Collection }

declare matches: collection {
    sourceIP: string,
    pageRank: int,
    adRevenue: real
} @ { Collection }

trigger aggregate_local: () = \_ -> (
    userVisits.iterate (
        \u -> rankings.iterate (
            \r -> if u.destURL == r.pageURL and lowerDate < u.visitDate and u.visitDate < upperDate
                    then matches.insert { sourceIP: u.sourceIP, pageRank: r.pageRank, adRevenue: u.adRevenue }
                    else ()
        )
    );

    let local_aggregate = matches.groupBy
        (\r -> r.sourceIP)
        (\a -> \r -> { count: a.count + 1, total: a.total + r.pageRank, revenue: a.revenue + r.adRevenue })
        { count: 0, total: 0, revenue: 0 }

    in (aggregate_global, master) <- local_aggregate
)

declare local_aggregate_complete: mut int = 0

declare global_partial_result: collection {
    key: string,
    value: { count: int, total: int, revenue: int }
} @ { Collection }

declare global_result: mut { sourceIP: string, avgRank: real, totalRevenue: int }
    = { sourceIP: "", avgRank: 0.0, totalRevenue: 0 }

declare insertWith: forall a, b. a -> b -> (b -> b) -> collection { key: a, value: b } @ { Collection } -> ()

trigger aggregate_global: collection {
    key: string,
    value: { count: int, total: int, revenue: int }
} @ { Collection } = \c -> (
    (c.iterate (
        \r -> bind r as { key: k, value: v } in (
                insertWith k {count: v.count, total: v.total, revenue: v.revenue}
                (\u -> {
                    count: u.count + v.count,
                    total: u.total + v.total,
                    revenue: u.revenue + v.revenue
                })
                global_partial_result)
    ));
    (local_aggregate_complete = local_aggregate_complete + 1);
    (if local_aggregate_complete == peer_count
        then global_partial_result.iterate (
                \g -> bind g as { key: k, value: v } in
                        if v.revenue > global_result.totalRevenue
                            then global_result = {
                                sourceIP: k,
                                avgRank: v.total / v.count,
                                totalRevenue: v.revenue
                            }
                            else ()
            )
        else ()
    )
)
