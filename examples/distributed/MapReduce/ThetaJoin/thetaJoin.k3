/*
 * thetaJoin.k3
 *
 * Created by Kartik Thapar on 02/19/2014 at 12:21:03
 * Copyright (c) 2014 Kartik Thapar. All rights reserved.
 *
 */

include "Annotation/Collection.k3"
include "Core/Builtins.k3"

/* DATASETS */

declare sTable : collection {FirstName : string, Age : int, LastName : string} @ {Collection}
declare tTable : collection {Name : string, Age : int, LastName : string} @ {Collection}
declare sNumRows : mut int = 0
declare tNumRows : mut int = 0
declare totalRows : int = 12
declare rowCounter : mut int = 0

/* MASTER */

declare master : address = 127.0.0.1:40000

/* MAPPER */

declare sMappers : mut collection {Address : address, Id : int} @ {Collection}
declare tMappers : mut collection {Address : address, Id : int} @ {Collection}

declare sNumMappers : int = 4
declare tNumMappers : int = 4
declare sCurrentMapperSize : mut int = 0
declare tCurrentMapperSize : mut int = 0

declare currentMapperSize : mut int = 0
declare sMapperOutput : collection {RegionId : int, Output : collection {FirstName : string, Age : int, LastName : string} @ {Collection}} @ {Collection}
declare tMapperOutput : collection {RegionId : int, Output : collection {Name : string, Age : int, LastName : string} @ {Collection}} @ {Collection}

declare _randomRow : mut int = 0
declare _randomColumn : mut int = 0

/* REDUCER */

/* Number of Reducers is equal to the total of Regions in the grid */
declare reducers : mut collection {Address : address, Id : int} @ {Collection}
declare numReducers : int = 9

declare sTuples : collection {FirstName : string, Age : int, LastName : string} @ {Collection}
declare tTuples : collection {Name : string, Age : int, LastName : string} @ {Collection}

/* REGIONS */

declare maxS : int = 2
declare maxT : int = 2
declare rowLookupTable : mut collection {Row : int, Regions : mut collection {RegionId : int} @ {Collection}} @ {Collection}
declare columnLookupTable : mut collection {Column : int, Regions : collection {RegionId : int} @ {Collection}} @ {Collection}

/* OUTPUT */

declare output : collection {sRow : {FirstName : string, Age : int, LastName : string}, tRow : {Name : string, Age : int, LastName : string}} @ {Collection}

/* PHASE 1: PRE-PROCESS 1 - Process Table Rows */

trigger preProcessTableRows : (collection {fromS : bool, sRow : option {FirstName : string, Age : int, LastName : string}, tRow : option {Name : string, Age : int, LastName : string}} @ {Collection}) = \tableElementCollection -> (
    tableElementCollection.iterate (\tableElement -> (
        rowCounter = rowCounter + 1;
        bind tableElement as {fromS : isItFromS, sRow : sRowElement, tRow : tRowElement} in
        if isItFromS then
            sNumRows = sNumRows + 1;
            case sRowElement of 
            {Some x -> sTable.insert {FirstName : x.FirstName, Age : x.Age, LastName : x.LastName}}
            {None -> ()}
        else
            tNumRows = tNumRows + 1;
            case tRowElement of
            {Some x -> tTable.insert {Name : x.Name, Age : x.Age, LastName : x.LastName}}
            {None -> ()}
    ));

    /* If all the rows have been processed, process regions. */
    if rowCounter == totalRows then (preProcessRegions, me) <- ()
    else ()
)

/* PHASE 1: PRE-PROCESS 2 - Process Regions*/

declare _tmpRegions : mut collection {RegionId : int} @ {Collection}

declare getSRegionsWorker : immut (collection {RegionId : int} @ {Collection} -> int -> int -> collection {RegionId : int} @ {Collection}) = fun tmpRegionsX -> fun regionNumber -> fun count -> (
    /* 
     * tmpRegionsX is just the carrier collection for RegionIds; count refers to max number of regions in a row which are added to the regionNumber iteratively.
     * Until count > 0, keep inserting regionNumber + count as a RegionId 
     */
    if count > 0 then
        tmpRegionsX.insert {RegionId : regionNumber + count};
        (getSRegionsWorker tmpRegionsX regionNumber (count - 1))
    else
        tmpRegionsX
)

declare getSRegions : immut (collection {RegionId : int} @ {Collection} -> int -> collection {RegionId : int} @ {Collection}) = fun tmpRegionsX -> fun rowNumber -> (
    (getSRegionsWorker tmpRegionsX ((rowNumber - 1)/maxS * tNumRows/maxT) (tNumRows/maxT))
)

declare getTRegionsWorker : immut (collection {RegionId : int} @ {Collection} -> int -> int -> collection {RegionId : int} @ {Collection}) = fun tmpRegionsX -> fun regionNumber -> fun count -> (
    /* 
     * count is a multiplier to the max number of regions in a row;
     * At every point we check if the regionNumber + thisvalue is greater than total number of regions possible and add to region list if not.
     */
    if (regionNumber + count*(tNumRows/maxT)) <= (sNumRows/maxS * tNumRows/maxT) then
        tmpRegionsX.insert {RegionId : regionNumber + count*(tNumRows/maxT)};
        (getTRegionsWorker tmpRegionsX regionNumber (count + 1))
    else
        tmpRegionsX
)

declare getTRegions : immut (collection {RegionId : int} @ {Collection} -> int -> collection {RegionId : int} @ {Collection}) = fun tmpRegionsX -> fun columnNumber -> (
    (getTRegionsWorker tmpRegionsX ((columnNumber - 1)/maxT + 1) 0)
)

declare preProcessRegionsWorker : mut (int -> string -> ()) = fun numberOfRows -> fun tableName -> (
    /* 
     * Process every single row from the last row to the first row; numberOfRows is just the max row count; but essentially the row number.
     * Create an empty collection always (by searching for a RegionId that does not exist - hack for clearing a list).
     * Find regions and then update them for every row/column.
     */
    if numberOfRows > 0 then
        if tableName == "S" then
            _tmpRegions = _tmpRegions.filter (\region -> region.RegionId == -127);
            _tmpRegions = (getSRegions _tmpRegions numberOfRows);
            rowLookupTable.insert {Row : numberOfRows, Regions : _tmpRegions};
            (preProcessRegionsWorker (numberOfRows - 1) "S")
        else
            _tmpRegions = _tmpRegions.filter (\region -> region.RegionId == -1);
            _tmpRegions = (getTRegions _tmpRegions numberOfRows);
            columnLookupTable.insert {Column : numberOfRows, Regions : _tmpRegions};
            (preProcessRegionsWorker (numberOfRows - 1) "T")
    else ()
)

trigger preProcessRegions : () = \_ -> (
    (preProcessRegionsWorker sNumRows "S");
    (preProcessRegionsWorker tNumRows "T");
    (arrangeMap, me) <- ()
)

/* PHASE 2: MAP */

declare getRandom : immut (string -> int) = fun tableName -> (
    if tableName == "S" then (random (sNumRows - 1) + 1)
    else (random (tNumRows - 1) + 1)
)

declare getMapperSize : immut (int -> string -> int) = fun mapperId -> fun tableName -> (
    /*
     * If r == 0 => equal division
     * If mapperId <= r => some r mappers will get the extra 1 row
     * If mapperId > r => will not get extra 1 row
     */
    if tableName == "S" then
        let r = sNumRows % sNumMappers in
            let q = sNumRows / sNumMappers in
                if r == 0 then q else if mapperId <= r then q + 1 else q
    else
        let r = tNumRows % tNumMappers in
            let q = tNumRows / tNumMappers in
                if r == 0 then q else if mapperId <= r then q + 1 else q
)

trigger understandData : (collection {Row : int, Regions : mut collection {RegionId : int} @ {Collection}} @ {Collection}, collection {Column : int, Regions : mut collection {RegionId : int} @ {Collection}} @ {Collection}, collection {Address : address, Id : int} @ {Collection}, int, int) = \data -> (
    bind data as (_rowLookupTable, _columnLookupTable, _reducers, _sNumRows, _tNumRows) in (
        rowLookupTable = _rowLookupTable;
        columnLookupTable = _columnLookupTable;
        reducers = _reducers;
        sNumRows = _sNumRows;
        tNumRows = _tNumRows
    )
)

trigger arrangeMap : () = \_ -> (
    /*
     * Copy data to other mappers
     * Sending lookup tables and a list of reducers to each mapper as output needs to be routed to all reducers irrespective of the data;
     * also the reducers are created on fly using a script; therefore sending a collection of reducer addresses seems ideal. 
     */

    sMappers.iterate (\mapper -> (
        (understandData, mapper.Address) <- (rowLookupTable, columnLookupTable, reducers, sNumRows, tNumRows)
    ));

    tMappers.iterate (\mapper -> (
        (understandData, mapper.Address) <- (rowLookupTable, columnLookupTable, reducers, sNumRows, tNumRows)
    ));

    /* Send rows to specific mappers */
    sTable.fold (\mapperCounter -> \row -> (
        (sendRowToMapper_s, me) <- (row, (mapperCounter % sNumMappers) + 1);
        mapperCounter + 1
    )) 0;

    tTable.fold (\mapperCounter -> \row -> (
        (sendRowToMapper_t, me) <- (row, (mapperCounter % sNumMappers) + 1);
        mapperCounter + 1
    )) 0
)

/*
 * Feedback:
 * 1. Not sure if this is the right approach to send the rows to different mappers one by one -> network congestion.
 * 2. Create rowOutputForMapper collection to collect mapper rows for each mapper.
 */

trigger sendRowToMapper_s : ({FirstName : string, Age : int, LastName : string}, int) = \x -> (
    bind x as (sRow, mapperId) in (
        let mapperSize = (getMapperSize mapperId "S") in
            let fMappers = sMappers.filter (\mapper -> (mapper.Id == mapperId)) in
                let mapperInstance = fMappers.peek() in
                    case mapperInstance of 
                    {Some mapper -> (map_s, mapper.Address) <- (sRow, mapperSize)}
                    {None -> ()}
    )
)

trigger sendRowToMapper_t : ({Name : string, Age : int, LastName : string}, int) = \x -> (
    bind x as (tRow, mapperId) in (
        let mapperSize = (getMapperSize mapperId "T") in
            let fMappers = tMappers.filter (\mapper -> (mapper.Id == mapperId)) in
                let mapperInstance = fMappers.peek() in
                    case mapperInstance of
                    {Some mapper -> (map_t, mapper.Address) <- (tRow, mapperSize)}
                    {None -> ()}
    )
)

/* Implement Mapper Functionality */

declare _sTmpMapperOutputCollection : mut collection {FirstName : string, Age : int, LastName : string} @ {Collection}
declare _tTmpMapperOutputCollection : mut collection {Name : string, Age : int, LastName : string} @ {Collection}

/* Debug Variables */

declare _randoms : collection {rval : int} @ {Collection}
declare _dsrows : collection {srow : {FirstName : string, Age : int, LastName : string}} @ {Collection}
declare _dtrows : collection {trow : {Name : string, Age : int, LastName : string}} @ {Collection}

trigger map_s : ({FirstName : string, Age : int, LastName : string}, int) = \x -> (
    bind x as (sRow, mapperSize) in (
        sCurrentMapperSize = sCurrentMapperSize + 1;
        _randomRow = (getRandom "S");
        _randoms.insert {rval : _randomRow};
        _dsrows.insert {srow : sRow};
        /*
         * Find the specific row in the lookup table.
         * Lookuo all the regions associated to that row.
         * Find the mapper output associated with that region using the RegionId field.
         * Create a new output if output is None; otherwise, insert the row in the existing output and update collection.
         */
        let optLookupRow = (rowLookupTable.filter (\lookupRow -> lookupRow.Row == _randomRow)).peek() in
            case optLookupRow of
            {Some lookupRow -> (
                lookupRow.Regions.iterate (\region -> (
                    let optOldOutput = (sMapperOutput.filter (\output -> output.RegionId == region.RegionId)).peek() in
                        case optOldOutput of
                        {Some oldOutput -> (
                            let newOutput = oldOutput.Output in
                                newOutput.insert {FirstName : sRow.FirstName, Age : sRow.Age, LastName : sRow.LastName};
                                sMapperOutput.update {RegionId : region.RegionId, Output : oldOutput.Output} {RegionId : region.RegionId, Output : newOutput}
                        )}
                        {None -> (
                            _sTmpMapperOutputCollection = _sTmpMapperOutputCollection.filter (\outputCollection -> outputCollection.Age == -1);
                            _sTmpMapperOutputCollection.insert {FirstName : sRow.FirstName, Age : sRow.Age, LastName : sRow.LastName};
                            sMapperOutput.insert {RegionId : region.RegionId, Output : _sTmpMapperOutputCollection}
                        )}
                ))
            )}
            {None -> ()};

        /* After computing on mapperSize amount of rows, we then send the data to the reudcers */
        if sCurrentMapperSize == mapperSize then (sendAllToReducer, me) <- ()
        else ()
    )
)

trigger map_t : ({Name : string, Age : int, LastName : string}, int) = \x -> (
    bind x as (tRow, mapperSize) in (
        tCurrentMapperSize = tCurrentMapperSize + 1;
        _randomColumn = (getRandom "T");
        _randoms.insert {rval : _randomColumn};
        _dtrows.insert {trow : tRow};
        /*
         * Find the specific column in the lookup table.
         * Lookuo all the regions associated to that column.
         * Find the mapper output associated with that region using the RegionId field.
         * Create a new output if output is None; otherwise, insert the column(row) in the existing output and update collection.
         */
        let optLookupColumn = (columnLookupTable.filter (\lookupColumn -> lookupColumn.Column == _randomColumn)).peek() in
            case optLookupColumn of
            {Some lookupColumn -> (
                lookupColumn.Regions.iterate (\region -> (
                    let optOldOutput = (tMapperOutput.filter (\output -> output.RegionId == region.RegionId)).peek() in
                        case optOldOutput of
                        {Some oldOutput -> (
                            let newOutput = oldOutput.Output in
                                newOutput.insert {Name : tRow.Name, Age : tRow.Age, LastName : tRow.LastName};
                                tMapperOutput.update {RegionId : region.RegionId, Output : oldOutput.Output} {RegionId : region.RegionId, Output : newOutput}
                        )}
                        {None -> (
                            _tTmpMapperOutputCollection = _tTmpMapperOutputCollection.filter (\outputCollection -> outputCollection.Age == -1);
                            _tTmpMapperOutputCollection.insert {Name : tRow.Name, Age : tRow.Age, LastName : tRow.LastName};
                            tMapperOutput.insert {RegionId : region.RegionId, Output : _tTmpMapperOutputCollection}
                        )}
                ))
            )}
            {None -> ()};

        /* After computing on mapperSize amount of rows, we then send the data to the reudcers */
        if tCurrentMapperSize == mapperSize then (sendAllToReducer, me) <- ()
        else ()
    )
)

/* PHASE 3: REDUCER */

declare _sEmptyCollection : collection {FirstName : string, Age : int, LastName : string} @ {Collection}
declare _tEmptyCollection : collection {Name : string, Age : int, LastName : string} @ {Collection}

trigger sendAllToReducer : () = \_ -> (
    /*
     * 1. This method is called by every mapper once : (sendAllToReducer, me) <- (). Therefore every mapper sends something to all reducers.
     * 2. For every reducer, check if the mapper has some output for it. If so, send the output; else send () data.
     */
    reducers.iterate (\reducer -> (
        let mapperOutputCorrespondingToReducer = (sMapperOutput.filter (\mapperOutput -> mapperOutput.RegionId == reducer.Id)).peek() in
            case mapperOutputCorrespondingToReducer of
            {Some mapperOutput -> (reduce_s, reducer.Address) <- mapperOutput.Output}
            {None -> (reduce_s, reducer.Address) <- _sEmptyCollection};

        let mapperOutputCorrespondingToReducer = (tMapperOutput.filter (\mapperOutput -> mapperOutput.RegionId == reducer.Id)).peek() in
            case mapperOutputCorrespondingToReducer of
            {Some mapperOutput -> (reduce_t, reducer.Address) <- mapperOutput.Output}
            {None -> (reduce_t, reducer.Address) <- _tEmptyCollection}
    ))

)

declare _sMapperReceiveCounter : mut int = 0
declare _tMapperReceiveCounter : mut int = 0

trigger reduce_s : (collection {FirstName : string, Age : int, LastName : string} @ {Collection}) = \coll -> (
    _sMapperReceiveCounter = _sMapperReceiveCounter + 1;
    coll.iterate (\row -> (
        sTuples.insert {FirstName : row.FirstName, Age : row.Age, LastName : row.LastName}
    ));
    let totalMappers = sNumMappers + tNumMappers in
        if (_sMapperReceiveCounter == totalMappers and _tMapperReceiveCounter == totalMappers) then
            (reduceDoJoin, me) <- ()
        else ()
)

trigger reduce_t : (collection {Name : string, Age : int, LastName : string} @ {Collection}) = \coll -> (
    _tMapperReceiveCounter = _tMapperReceiveCounter + 1;
    coll.iterate (\row -> (
        tTuples.insert {Name : row.Name, Age : row.Age, LastName : row.LastName}
    ));
    let totalMappers = sNumMappers + tNumMappers in
        if (_sMapperReceiveCounter == totalMappers and _tMapperReceiveCounter == totalMappers) then
            (reduceDoJoin, me) <- ()
        else ()
)

declare reducerOutput : collection {sRow : {FirstName : string, Age : int, LastName : string}, tRow : {Name : string, Age : int, LastName : string}} @ {Collection}

trigger reduceDoJoin : () = \_ -> (
    sTuples.iterate (\sRow -> (
        tTuples.iterate (\tRow -> (
            if sRow.Age == tRow.Age then reducerOutput.insert {sRow : sRow, tRow : tRow}
            else ()
        ))
    ));

    (accumulateOutput, master) <- (reducerOutput)
)

/* PHASE 4: ACCUMULATE OUTPUT */

trigger accumulateOutput : (collection {sRow : {FirstName : string, Age : int, LastName : string}, tRow : {Name : string, Age : int, LastName : string}} @ {Collection}) = \reducerOutput -> (
    reducerOutput.iterate (\outputRecord -> (
        output.insert {sRow : outputRecord.sRow, tRow : outputRecord.tRow}
    ))
)

source master : (collection {fromS : bool, sRow : option {FirstName : string, Age : int, LastName : string}, tRow : option {Name : string, Age : int, LastName : string}} @ {Collection}) = file "/Users/kartikthapar/WorkCenter/Projects/K3/core/examples/distributed/MapReduce/ThetaJoin/tables.text" k3

feed master |> preProcessTableRows
