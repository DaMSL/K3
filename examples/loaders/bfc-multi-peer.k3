include "Annotation/Collection.k3"
include "Annotation/BulkFlatCollection.k3"
include "Annotation/Maps/IntMap.k3"
include "Annotation/Set.k3"
include "Core/Builtins.k3"
include "Core/Profile.k3"

// TODO handle @:TPCHDate
typedef lineitem_r = {
      l_orderkey         : int,
      l_partkey          : int,
      l_suppkey          : int,
      l_linenumber       : int,
      l_quantity         : real,
      l_extendedprice    : real,
      l_discount         : real,
      l_tax              : real,
      l_returnflag       : string,
      l_linestatus       : string,
      l_shipdate         : string,
      l_commitdate       : string,
      l_receiptdate      : string,
      l_shipinstruct     : string,
      l_shipmode         : string,
      l_comments         : string
}

// Globals and Loader
declare lineitem_local: mut collection {elem: lineitem_r} @Collection
declare lineitem_empty: collection {elem: lineitem_r} @Collection
declare lineitemLoaderPE: collection {path: string} @Collection -> collection {elem: lineitem_r} @Collection -> ()
declare files: collection {path: string} @Collection

declare lineitem_bfc : mut collection lineitem_r @BulkFlatCollection
declare lineitem_bfc2 : mut collection lineitem_r @BulkFlatCollection

declare receivers: collection {addr: address} @Collection
declare batch_size: int = 100000

trigger convert: () = \_ -> (
 lineitemLoaderPE files lineitem_local;
 let last_batch =
   lineitem_local.fold (\acc -> \e -> (
     if acc.size() < batch_size
     then (
       acc.insert e;
       acc
     )
     else (
       let bfc = empty lineitem_r @BulkFlatCollection
       in (
         bfc.append acc;
         (sink1, me) <- bfc.save();
         lineitem_empty  
       )
     )
   )) lineitem_empty
  in (
    let bfc = empty lineitem_r @BulkFlatCollection
    in (
      bfc.append last_batch;
      (sink1, me) <- bfc.save();
      (stop, me) <- ()
    )
  )
)

trigger stop: () = \_ -> (
  haltEngine()
)

trigger read_start: () = \_ -> (
  openFile me "input" "/local/data/batches.in" "raw" false "r";
  (read_loop, me) <- 0
)

trigger read_loop: int = \i -> (
  if hasRead me "input"
  then (
    (receive, (receivers.at  i).addr) <- doRead me "input";
    (read_loop, me) <- if i+1 < receivers.size() then i+1 else 0
  )
  else (
    receivers.iterate (\p -> (stop, p.addr) <- () );
    (stop, me) <- ()
  )
)

declare local_rows: mut int = 0
trigger receive: string = \s -> (
  let bfc =  (empty lineitem_r @BulkFlatCollection)
  in (
    bfc.load(s);
    local_rows = local_rows + (bfc.fold (\a -> \_ -> a + 1) 0)
  )
)

source s1: () = value ()
feed s1 |> convert

source s2: () = value ()
feed s2 |> read_start

sink sink1: string = file "/local/data/batches.out" binary raw
